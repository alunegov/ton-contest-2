
int preload_grams(slice s) asm "LDGRAMS" "DROP";

(cell, int, int) load_data() {
    slice data = get_data().begin_parse();
    cell round = data~load_dict();
    int seqno = data~load_uint(32);
    int owner = data~load_uint(256);
    data.end_parse();
    return (round, seqno, owner);
}

() save_data(cell round, int seqno, int owner) impure {
    set_data(begin_cell()
        .store_dict(round)
        .store_uint(seqno, 32)
        .store_uint(owner, 256)
        .end_cell());    
}

_ unpack_round(cell round) {
    var rs = round.begin_parse();
    var res = (rs~load_grams(), rs~load_dict());
    rs.end_parse();
    return res;
}

cell pack_round(int prize_fund, cell participants) {
    return begin_cell()
        .store_grams(prize_fund)
        .store_dict(participants)
        .end_cell();
}

() recv_internal(int msg_value, cell in_msg_cell, slice in_msg) impure {
    var cs = in_msg_cell.begin_parse();
    var flags = cs~load_uint(4);  ;; int_msg_info$0 ihr_disabled:Bool bounce:Bool bounced:Bool
    var s_addr = cs~load_msg_addr();

    if ((flags & 1) | in_msg.slice_empty?()) {
        ;; a bounced message, or a simple transfer
        return ();
    }

    int op = in_msg~load_uint(32);

    if (op == 1) {
        ;; add new ticket
        var (wc, addr) = parse_std_addr(s_addr);
        ;;throw_unless(39, wc == 0);
        ;;throw_unless(40, addr == 1);

        var (n1, n2, n3) = (in_msg~load_uint(8), in_msg~load_uint(8), in_msg~load_uint(8));

        var (round, seqno, owner) = load_data();

        var nums = begin_cell()
            .store_uint(n1, 8)
            .store_uint(n2, 8)
            .store_uint(n3, 8);

        var prize_fund = 0;
        var participants = new_dict();
        ifnot (round.null?()) {
            (prize_fund, participants) = unpack_round(round);
        }
        
        participants~udict_set_builder(256, addr, nums);

        round = pack_round(prize_fund + msg_value, participants);

        save_data(round, seqno, owner);

        return();
    }

    if (op == 2) {
        ;; inc prize fund
        var (round, seqno, owner) = load_data();
        
        ifnot (round.null?()) {
            var (prize_fund, participants) = unpack_round(round);

            round = pack_round(prize_fund + msg_value, participants);
        } else {
            round = pack_round(msg_value, new_dict());
        }

        save_data(round, seqno, owner);

        return();
    }

    ;; if op is non-zero and its higher bit is zero, throw an exception (the message is an unsupported query) to
    ;; bounce message back to sender
    throw_unless(37, (op == 0) | (op & (1 << 31)));
    ;; do nothing for other internal messages    
}

() recv_external(slice in_msg) impure {
    var signature = in_msg~load_bits(512);
    var cs = in_msg;
    var (msg_seqno, valid_until, op) = (cs~load_uint(32), cs~load_uint(32), cs~load_uint(32));
    throw_if(35, valid_until <= now());
    var (round, seqno, owner) = load_data();
    throw_unless(33, msg_seqno == seqno);
    throw_unless(34, check_signature(slice_hash(in_msg), signature, owner));
    accept_message();
    if (op == 1) {
        cs~touch();
        while (cs.slice_refs()) {
            var mode = cs~load_uint(8);
            send_raw_message(cs~load_ref(), mode);
        }
        cs.end_parse();
    ;;} else if (op == 2) {
    } else {
        throw_unless(40, op == 9);  ;; 9 - op for init
    }
    save_data(round, seqno + 1, owner);
}

;; Get methods

int seqno() method_id {
    return get_data().begin_parse().skip_dict().preload_uint(32);
}

int prize_fund() method_id {
    cell round = get_data().begin_parse().preload_dict();
    return round.null?() ? 0 : round.begin_parse().preload_grams();
}
