"TonUtil.fif" include

{ ."usage: " @' $0 type ." <filename-base> <seqno> [<savefile>]" cr
  ."Creates a conduct lottery requrest to lottery created by lottery-new.fif with private key loaded from" cr
  ."<filename-base>.pk and address from <filename-base>.addr. Saves it to <savefile>.boc ('lottery-conduct-query'" cr
  ."by default)" cr 1 halt
} : usage
$# dup 2 < swap 3 > or ' usage if

120 =: timeout   // external message expires in 120 seconds

$1 =: file-base
$2 parse-int =: seqno
def? $3 { @' $3 } { "lottery-conduct-query" } cond constant savefile

file-base +".addr" load-address-verbose 2constant lottery_addr
file-base +".pk" load-keypair nip constant lottery_pk

// msg_seqno, valid_until, op
<b seqno 32 u, -1 32 i, 2 32 u, b>

dup ."signing message: " <s csr. cr
dup hashu lottery_pk ed25519_sign_uint

<b b{1000100} s, lottery_addr addr, 0 Gram, b{00} s, swap B, swap <s s, b>
dup ."External message for conduct lottery is " <s csr. cr

2 boc+>B dup Bx. cr
savefile +".boc" tuck B>file
."(Saved conduct lottery query to file " type .")" cr
